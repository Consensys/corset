WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = { ";" ~ (!NEWLINE ~ ANY)* }


corset = { SOI ~ (definition)* ~ EOI }

definition = { "(" ~ definition_kw ~ (sexpr | expr)* ~ ")"}
definition_kw = { "module" | "defconstraint" | "defunalias" | "defun" | "defpurefun" | "defconst" | "defalias" | "defplookup" | "defpermutation" | "definrange" | "defperspective" | "defcolumns" }

sexpr = { "(" ~ (expr | keyword | range)* ~ ")" }
expr = { integer | symbol | sexpr | form | nth }

form = _{ forloop }

forloop = { "(" ~ "for" ~ symbol ~ range ~ expr ~ ")" }

nth = { "[" ~ symbol ~ expr ~ "]" }
range = _{ immediate_range | interval }
immediate_range = { "{" ~ integer+ ~ "}" }
interval = { "[" ~ natural ~ (":" ~ natural ~ (":" ~ natural)?)? ~ "]" }
typing = @{ "NATURAL" | "BOOLEAN" | "BYTE" | "NIBBLE" }

integer = @{ "-"? ~ natural }
natural = @{ "0x" ~ ASCII_HEX_DIGIT+ | "0b" ~ ASCII_BIN_DIGIT+ | ASCII_DIGIT+ }
symbol = @{ (LETTER | NUMBER | SYMBOL | "-" | "*" | "=" | "_" | "," | "." | "'" | "/" | "!")+ }
keyword = @{ ":" ~ (LETTER | NUMBER | SYMBOL | "-" | "*" | "=" | "_" )+  }
