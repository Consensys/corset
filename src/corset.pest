WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }


corset = { SOI ~ (top_level)* ~ EOI }

constraint = { sexpr }

top_level = _{ defcolumns | definition | defmodule }

definition = { "(" ~ definition_kw ~ (sexpr | expr)* ~ ")"}
definition_kw = { "defconstraint" | "defunalias" | "defun" | "defpurefun" | "defconst" | "defalias" | "defplookup" | "defpermutation" | "definrange" }
defmodule = {"(module" ~ symbol ~ ")"}


defcolumns = { "(" ~ "defcolumns" ~ defcolumn* ~ ")" }
defcolumn = { symbol | init_sexpr }
computed = { computation ~ "[" ~ expr+ ~ "]" }
computation = _{ ":INTERLEAVED" | ":COMP" }



sexpr = { "(" ~ (expr | keyword | range)* ~ ")" }
init_sexpr = _{ "(" ~ symbol ~ (expr | keyword | range)* ~ ")" }
expr = { integer | symbol | sexpr | form }
form = _{ forloop }

forloop = { "(" ~ "for" ~ symbol ~ range ~ expr ~ ")" }
range = _{ immediate_range | interval }
immediate_range = { "{" ~ integer+ ~ "}" }
interval = { "[" ~ natural ~ (":" ~ natural ~ (":" ~ natural)?)? ~ "]" }
typing = @{ "NATURAL" | "BOOLEAN" | "BYTE" | "NIBBLE" }

integer = @{ "-"? ~ natural }
natural = @{ ASCII_DIGIT+ }
symbol = @{ (LETTER | NUMBER | SYMBOL | "-" | "*" | "=" | "_" | "," | "." | "'" )+ }
keyword = @{ ":" ~ (LETTER | NUMBER | SYMBOL | "-" | "*" | "=" | "_" )+  }
