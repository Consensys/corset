WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }


corset = { SOI ~ (top_level)* ~ EOI }

constraint = { list }

top_level = _{ defcolumns | definition | defmodule }

definition = { "(" ~ definition_kw ~ expr* ~ ")"}
definition_kw = { "defconstraint" | "defunalias" | "defun" | "defconst" | "defalias" | "defplookup" | "defpermutation" }
defmodule = {"(module" ~ symbol ~ ")"}


defcolumns = { "(" ~ "defcolumns" ~ defcolumn* ~ ")" }
defcolumn = { symbol | init_list }
computed = { ":" ~ computation ~ "[" ~ expr+ ~ "]" }
computation = _{ "SORTED" | "INTERLEAVED" | "COMP" }



list = { "(" ~ expr* ~ ")" }
init_list = _{ "(" ~ symbol ~ (expr | keyword | range)* ~ ")" }
expr = { integer | symbol | list | form }
form = _{ forloop }

forloop = { "(" ~ "for" ~ symbol ~ range ~ expr ~ ")" }
range = _{ immediate_range | interval }
immediate_range = { "{" ~ natural+ ~ "}" }
interval = { "[" ~ natural ~ (":" ~ natural ~ (":" ~ natural)?)? ~ "]" }
typing = @{ "NATURAL" | "BOOLEAN" }

integer = @{ "-"? ~ natural }
natural = @{ ASCII_DIGIT+ }
symbol = @{ (LETTER | NUMBER | SYMBOL | "-" | "*" | "=" | "_" | "," )+  }
keyword = @{ ":" ~ (LETTER | NUMBER | SYMBOL | "-" | "*" | "=" | "_" )+  }
